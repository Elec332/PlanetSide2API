plugins {
    id "java-library"
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id "nl.elec332.gradle.ossrhplugin" version "1.1.0"
}

group 'nl.elec332.planetside2'
version '1.0-Beta4'

sourceCompatibility = targetCompatibility = 9

repositories {
    mavenCentral()
}

configurations {
    merge
}

configurations.compileOnly.extendsFrom(configurations.merge)

dependencies {
    api "com.google.code.gson:gson:2.8.6"
    merge "com.neovisionaries:nv-websocket-client:2.14"
}

ossrh {
    description = 'Java implementation/wrapper for the DGB Census API for PlanetSide2'
    githubUrl = 'https://github.com/Elec332/PlanetSide2API'

    license {
        name = 'GNU Lesser General Public License, version 2.1'
        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
    }
    developer {
        name = 'Elec332'
    }
}

javadoc {
    modularity.inferModulePath = false
}

shadowJar {
    configurations = [project.configurations.merge]
    relocate "com.neovisionaries", "rl.ps2.socket"
    minimize()
    getExcludes().remove("module-info.class")
    doLast {
        components.java.withVariantsFromConfiguration(project.configurations.shadowRuntimeElements, {a ->
            a.skip()
        })
    }
}

compileJava {
    options.compilerArgs += ['--add-reads', 'nl.elec332.planetside2.api=ALL-UNNAMED']
}

tasks.jar.finalizedBy(shadowJar)
tasks.shadowJar.archiveClassifier = ""
